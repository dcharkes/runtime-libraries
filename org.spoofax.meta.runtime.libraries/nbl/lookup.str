module nbl/lookup

imports
  
  nbl/collect
  nbl/query
  nbl/tasks
  nbl/uri
  nbl/entries
  task/core

rules // Lookup
	
	nabl-lookup(|ns, name) = 
		nabl-uri; nabl-lookup-uri(|ns, name)
		
	nabl-lookup-uri(|ns, name):
		uri -> def*
		with
  		uri'  := <nabl-extend-uri(|ns, name, ())> uri; 
  		def*  := <nabl-get-all-definitions> uri'

  nabl-lookup-import(|ns, name) =
  	nabl-uri; nabl-lookup-import-uri(|ns, name)
			  		
	nabl-lookup-import-uri(|ns, name):
		scope -> import*
		with
			uri     := <nabl-uri> scope;
			import* := <nabl-lookup-import-one(|ns, name)> uri
			
	nabl-lookup-import-one(|ns, name):
		uri -> [namedImport*, unnamedImport*]
		with
			language       := <nabl-uri-language> uri;
      namedImport*   := <nabl-get-import-uris-named(|language, ns, name)> uri;
      unnamedImport* := <nabl-get-import-uris-unnamed(|language, ns)> uri

rules // Imports

  nabl-get-import-uris-unnamed(|language, namespace) = nabl-get-import-uris-unnamed(|[], language, namespace)

  nabl-get-import-uris-unnamed(|seen, language, namespace):
    uri -> [importURI*, transitiveImportURI*]
    with
      importResult*        := <nabl-get-all-properties(|Import(language, namespace))> uri;
      importURI*           := <map(task-get-solved <+ task-create-dependencies; ![<id>]); concat> importResult*;
      importedResult*      := <nabl-get-all-properties(|Import(language, Imported(namespace)))> uri;
      importedURI*         := <map(task-get-solved <+ task-create-dependencies; ![<id>]); concat> importedResult*;
      transitiveImportURI* := <mapconcat(nabl-get-import-uris-unnamed-transitive(|seen, language, namespace))> importedURI*

  nabl-get-import-uris-unnamed-transitive(|seen, language, namespace):
    uri -> transitiveImportURI*
    where
      not(<fetch(?uri)> seen);
      transitiveImportURI* := <nabl-get-import-uris-unnamed(|[uri|seen], language, namespace)> uri
    
  nabl-get-import-uris-named(|language, namespace, name):
    uri -> importURI*
    with
      importResult* := <nabl-get-all-properties(|Import(language, namespace, name))> uri;
      importURI*    := <map(task-get-solved <+ task-create-dependencies; ![<id>]); concat> importResult*
