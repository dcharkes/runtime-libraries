module types/collect

imports 
  task/-
  types/-
  nbl/-
  editor/-
  
strategies
  
  annotate-property-tasks(|task*) =
    !([Type()|<custom-properties <+ ![]>], <id>)
  ; foldl(annotate-property-task(|task*) <+ Snd)
  
  annotate-property-task(|task*):
    (kind, term{a*}) -> term{(kind, prop), a*}
    where
      not(<property-task(|kind)> term)
    where
      prop := <new-property-task(|task*)> 
     
  new-property-task(|task*):
    (Type(), term) -> <type-of(|task*)> term

  property-task(|kind) = 
    get-annos
  ; fetch-elem(?(kind, task))
  ; !task
 
  property-task(|kind, task*) = 
     property-task(|kind)
  <+ <new-property-task(|task*)> (kind, <id>)
    
strategies
  
  prop-calc(|task*, dep*) = 
    where(all-dep* := <union; debug(!"deps ")> (<dependent-tasks>, <dependent-tasks> dep*));
    <new-task(|task*, all-dep*)> PropCalc(<id>)
    
  prop-check(|kind, task*, dep*):
    (term, expected) -> task
    where
      actual    := <property-task(|kind)> term
    ; expected* := <try-make-list> expected
    ; check*    := <map(<new-task(|task*)> PropCheck(actual, <id>))> expected*
    ; task      := <new-task(|task*, <dependent-tasks> [dep*, check*])> Choice(check*)
  
  prop-check(|kind, task*, dep*):
    (term, expected, msg) -> task
    where
      task := <prop-check(|kind, task*, dep*)> (term, expected)
    ; <origin-track-forced(task-create-error-on-failure(|task*, task, msg))> term
  
 prop-lookup(|kind, task*, dep*) = 
    where(all-dep* := <union> (<dependent-tasks> dep*, <dependent-tasks>))
  ; <new-task(|task*, all-dep*)> PropLookup(kind, <id>) 
  
  prop-match(|task*, relation) = 
    <new-task(|task*)> Rewrite(relation, <id>)
    
  prop-match(|task*, relation, dep*) = 
    where(all-dep* := <union> (<dependent-tasks> dep*, <dependent-tasks>))
  ; <new-task(|task*)> Rewrite(relation, <id>)
  
strategies
  
  type-is(|task*)           = prop-calc(|task*, [])
  type-is(|task*, dep*)     = prop-calc(|task*, dep*)
  
  type-check(|task*)        = prop-check(|Type(), task*, [])
  type-check(|task*, dep*)  = prop-check(|Type(), task*, dep*)
  
  type-lookup(|task*)       = prop-lookup(|Type(), task*, [])
  type-lookup(|task*, dep*) = prop-lookup(|Type(), task*, dep*)
  
  type-match(|task*, relation) = prop-match(|task*, relation)

  type-of = property-task(|Type())
  